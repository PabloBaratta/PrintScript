plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.18.0'
    id 'checkstyle'
    id 'jacoco-report-aggregation'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    jacocoAggregation project(':lexer')
    jacocoAggregation project(':interpreter')
    jacocoAggregation project(':parser')
    jacocoAggregation project(':cli')
    jacocoAggregation project(':ast')
}

tasks.named('testCodeCoverageReport', JacocoReport) {
    doLast {
        def reportLocation = reports.html.outputLocation.get().asFile
        println("HTML report generated: file://${reportLocation.absolutePath}/index.html")
    }
}

tasks.named('check') {
    dependsOn testCodeCoverageReport
}

tasks.named('build') {
    dependsOn addPreCommitGitHook
}


test {
    useJUnitPlatform()
}

spotless {
	format 'misc', {
		target '**/*.java'
        targetExclude '**/build*/**'
        trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
}


checkstyle {
    toolVersion = '10.12.1' // You can specify the desired version
    config = resources.text.fromFile('config/checkstyle/checkstyle.xml')
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
        html.destination = file("${buildDir}/reports/checkstyle.html")
    }
}
tasks.register("addPreCommitGitHook") {
    println("Adding pre commit")
    doLast {
        exec {
            // Usar 'copy' en Windows
            if (System.getProperty("os.name").toLowerCase().contains("windows")) {
                commandLine("cmd", "/c", "copy", ".\\git-hooks\\pre-commit", ".\\.git\\hooks\\")
            } else {
                // Usar 'cp' en sistemas Unix/Linux
                commandLine("cp", "./git-hooks/pre-commit", "./.git/hooks")
                commandLine("chmod", "+x", "./.git/hooks/pre-commit")
            }
        }.assertNormalExitValue().rethrowFailure()
    }
}
